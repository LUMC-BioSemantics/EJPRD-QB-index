PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ejprd: <http://purl.org/ejp-rd/vocabulary/>

SELECT DISTINCT ?resource ?resource_name ?resource_endpoint ?specs_iri ?theme ?resource_description ?resource_logo {
    
    ?resource a dcat:Dataset;
              rdfs:label ?resource_name;
              dcat:theme ?theme, ejprd:VPQueryable.
    
    OPTIONAL {

       ?resource dcat:distribution ?resource_distribution.
       ?resource_distribution a dcat:Distribution;
                           dcat:accessService ?dataservice.
    
       ?dataservice a dcat:DataService;
                 dct:conformsTo ?specs_iri;
                 dcat:endpointURL ?resource_endpoint.
    
        FILTER (!regex(STR(?specs_iri), "/profile/","i")) .

    }    
    OPTIONAL {?resource dct:description ?resource_description}
    OPTIONAL {?resource dct:publisher [foaf:logo ?resource_logo]}
} ORDER BY ?resource
